apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.firebase.crashlytics'

// Create a variable called keystorePropertiesFile, and initialize it to your keystore.properties file, in the rootProject folder.
// And load the properties
def keystorePropertiesFile = rootProject.file("./keystore/keystore.properties")
def keystoreProperties = new Properties()
try {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (Exception e) {
    println("WARNING! Keystore files not found! KeystoreProperties couldn't be loaded")
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.handysparksoft.trackmap"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode getCustomVersionCode(referenceVersionCode)
        versionName getCustomVersionName(referenceVersionName)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [
                deeplinkScheme: "trackmap.page.link"
        ]
    }

    signingConfigs {
        config {
            try {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            } catch (Exception e) {
                println("WARNING! KeystoreProperties are not loaded!")
            }
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders = [crashlyticsCollectionEnabled: "false"]
        }

        release {
            manifestPlaceholders = [crashlyticsCollectionEnabled: "true"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    // Custom APK output name
    applicationVariants.all { variant ->
        def buildType = variant.buildType.name
        def versionCode = defaultConfig.versionCode
        def versionName = defaultConfig.versionName
        def apkOutputName = "app-${buildType}_trackmap_VC${versionCode}_${versionName}_.apk"

        variant.outputs.all { output ->
            outputFileName = apkOutputName
        }
    }

    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(":usecases")
    implementation project(":data")
    implementation project(":domain")

    implementation androidLibs.values()
    implementation kotlinLibs.values()
    implementation networkLibs.values()
    implementation firebaseLibs.values()
    implementation imageLibs.values()
    implementation miscLibs.values()
    implementation libs.values()

    kapt kaptLibs.values()

    testImplementation testLibs.values()
    androidTestImplementation androidTestLibs.values()
}

apply plugin: 'com.google.gms.google-services'
