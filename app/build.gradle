apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'io.fabric'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.handysparksoft.trackmap"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode getCustomVersionCode(referenceVersionCode)
        versionName getCustomVersionName(referenceVersionName)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Custom APK output name
    applicationVariants.all { variant ->
        def buildType = variant.buildType.name
        def versionCode = defaultConfig.versionCode
        def versionName = defaultConfig.versionName
        def apkOutputName = "app-${buildType}_trackmap_VC${versionCode}_${versionName}_.apk"

        variant.outputs.all { output ->
            outputFileName = apkOutputName
        }
    }

    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation androidLibs.values()
    implementation kotlinLibs.values()
    implementation networkLibs.values()
    implementation libs.values()

    testImplementation testLibs.values()
    androidTestImplementation androidTestLibs.values()
}

apply plugin: 'com.google.gms.google-services'
